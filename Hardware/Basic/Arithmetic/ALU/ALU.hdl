/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zero x,y
    Mux16 (a=x, b[0..15]=false, sel=zx, out=tmpxZ);
    Mux16 (a=y, b[0..15]=false, sel=zy, out=tmpyZ);

    // negative x,y
    Not16 (in=tmpxZ, out=notx);
    Not16 (in=tmpyZ, out=noty);

    Mux16 (a=tmpxZ, b=notx, sel=nx, out=tmpxZN);
    Mux16 (a=tmpyZ, b=noty, sel=ny, out=tmpyZN);

    // process func
    Add16 (a=tmpxZN, b=tmpyZN, out=addxy);
    And16 (a=tmpxZN, b=tmpyZN, out=andxy);
    
    Mux16 (a=andxy, b=addxy, sel=f, out=tmpoutF);

    // negate the output
    Not16 (in=tmpoutF, out=notout);
    Mux16 (a=tmpoutF, b=notout, sel=no, out=tmpoutFN);

    // zr compute
    Or16Way (in=tmpoutFN, out=notzr);
    Not (in=notzr, out=zr);

    // ng compute
    IsNeg16 (in=tmpoutFN, out=ng);

    Mux16 (a=tmpoutF, b=notout, sel=no, out=out); 
}